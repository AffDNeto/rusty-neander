; Testa subtracao ahmes
; zera a flag C antes dos testes
LDA ZERO
ADD ZERO
; Comentarios com as flags esperadas seguem a seguinte ordem (N,Z,V,C,B)

; (1) - (1) = 0, (0,1,0,0,0)
LDA ONE 
SUB ONE ;AC = 1 - 1 = 0 Z=1
STA T0
JNZ ERR
JN ERR
JV ERR 
JC ERR 
JB ERR 

; (-128)-(1)=+127, (0,0,1,0,0) overflow
LDA MINN; AC = 128
SUB ONE ; AC = 128-1 = 127, V=1,B=0
STA T1
JZ ERR
JN ERR
JC ERR
JB ERR
JNV ERR

; (0)-(-128)=-128, (1,0,1,0,1) overflow + borrow
LDA ZERO; AC = 0
SUB MINN; AC = 0-128 = 128 V=1,B=1
STA T2
JP ERR
JZ ERR
JC ERR
JNB ERR
JNV ERR

; (0)-(1)=255, (1,0,0,0,1) borrow
LDA ZERO; AC = 0
SUB ONE ; AC = 0 - 1 = 255
STA T3
JZ ERR
JP ERR
JC ERR
JNB ERR
JV ERR

JMP END
ERR: 
LDA ONE
STA E
END:
HLT

ORG 128
T0: 1
T1: 1
T2: 1
T3: 1
ZERO: 0	
ONE: 1
TWO: 2
MAX: 255; Valor maximo em byte
MAXN:127; Valor maximo em complemento de 2
MINN:128; Valor minimo negativo em complemento de 2
E: 0    ; Variavel para indicar que teve um erro no teste